apply plugin: 'android'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':BaseGameUtils')
    compile 'com.google.android.gms:play-services:4.3.+'
    compile 'com.android.support:appcompat-v7:19.+'
    compile 'com.jakewharton:butterknife:5.0.0'
    compile 'com.squareup:otto:1.3.4'
    compile('de.keyboardsurfer.android.widget:crouton:1.8.4') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "alpha 0.01"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {

        }
        debug {

        }
    }

    buildTypes {
        release {
            debuggable false
            zipAlign true
            runProguard false
        }
        debug {
            debuggable true
            zipAlign true
            runProguard false
        }
    }

    // Read our private signing details from the local properties file
    if (project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyAlias') &&
            project.hasProperty('keyPassword')) {
        println 'gradle.properties OK'
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.keyPassword = keyPassword

        buildTypes.release.signingConfig = android.signingConfigs.release
        // Because we require the Google Play games API to be working even in Debug, we must always sign
        // with out release cert
        buildTypes.debug.signingConfig = android.signingConfigs.release
    } else {
        println 'gradle.properties not well formed'
        if (!project.hasProperty('storeFile')) println 'storeFile not defined'
        if (!project.hasProperty('storePassword')) println 'storePassword not defined'
        if (!project.hasProperty('keyAlias')) println 'keyAlias not defined'
        if (!project.hasProperty('keyPassword')) println 'keyPassword not defined'

        buildTypes.release.signingConfig = null
    }
}